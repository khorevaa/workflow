#Использовать delegate

Перем ИндексОбработчиков;

Процедура Добавить(Знач ИмяСобытия, ОбъектОбработчик, Знач ИмяМетода) Экспорт
	
	ИндексОбработчиков.Вставить(ИмяСобытия, ОписаниеОбработчика(ОбъектОбработчик, ИмяМетода));
	
КонецПроцедуры

Функция Количество() Экспорт
	Возврат ИндексОбработчиков.Количество()
КонецФункции

Функция ПолучитьОбработчики() Экспорт
	Возврат ИндексОбработчиков;
КонецФункции

Функция ОписаниеОбработчика(Обработчик, ИмяМетода)
	
	ОписаниеОбработчика = Новый Структура();
	ОписаниеОбработчика.Вставить("Обработчик", Обработчик);
	ОписаниеОбработчика.Вставить("имяМетода", ИмяМетода);
	ОписаниеОбработчика.Вставить("Параметры", Новый Массив());
	
	Возврат ОписаниеОбработчика;
	
КонецФункции

Функция НовыеПараметры(Параметр1 = Неопределено, Параметр2 = Неопределено,
	Параметр3 = Неопределено, Параметр4 = Неопределено,
	Параметр5 = Неопределено, Параметр6 = Неопределено) Экспорт
	
	МассивПараметров = Новый Массив();
	
	Если Не Параметр1 = Неопределено Тогда
		МассивПараметров.Добавить(Параметр1);
	КонецЕсли;
	
	Если Не Параметр2 = Неопределено Тогда
		МассивПараметров.Добавить(Параметр2);
	КонецЕсли;
	
	Если Не Параметр3 = Неопределено Тогда
		МассивПараметров.Добавить(Параметр3);
	КонецЕсли;
	
	Если Не Параметр4 = Неопределено Тогда
		МассивПараметров.Добавить(Параметр4);
	КонецЕсли;
	
	Если Не Параметр5 = Неопределено Тогда
		МассивПараметров.Добавить(Параметр5);
	КонецЕсли;
	
	Если Не Параметр6 = Неопределено Тогда
		МассивПараметров.Добавить(Параметр6);
	КонецЕсли;
	
	Возврат МассивПараметров;
	
КонецФункции

Функция ПараметрыСобытия(Знач ИмяСобытия, Параметр1 = Неопределено, Параметр2 = Неопределено,
	Параметр3 = Неопределено, Параметр4 = Неопределено,
	Параметр5 = Неопределено, Параметр6 = Неопределено) Экспорт
	
	ОписаниеОбработчика = ИндексОбработчиков[ИмяСобытия];
	
	Если ОписаниеОбработчика = Неопределено Тогда
		ВызватьИсключение СтрШаблон("Событие <%1> - не найдено в индексе обработчиков", ИмяСобытия);
	КонецЕсли;
	
	МассивПараметров = НовыеПараметры(Параметр1, Параметр2,
				Параметр3, Параметр4,
				Параметр5, Параметр6);

	ОписаниеОбработчика.Параметры = МассивПараметров;

	Возврат МассивПараметров;
	
КонецФункции

Функция ПолучитьОписаниеСобытия(Знач ИмяСобытия) Экспорт
	
	ОписаниеОбработчика = ИндексОбработчиков[ИмяСобытия];
	
	Если ОписаниеОбработчика = Неопределено Тогда
		ВызватьИсключение СтрШаблон("Событие <%1> - не найдено в индексе обработчиков", ИмяСобытия);
	КонецЕсли;
	
	Возврат ОписаниеОбработчика;

КонецФункции

Процедура Выполнить_(Знач ИмяСобытия, Знач ПараметрыСобытия = Неопределено) Экспорт
	
	ОписаниеОбработчика = ИндексОбработчиков[ИмяСобытия];
	
	Если ОписаниеОбработчика = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Делегат = Делегаты.Создать(ОписаниеОбработчика.Обработчик, ОписаниеОбработчика.ИмяМетода);
	
	Если ПараметрыСобытия = Неопределено Тогда
		ПараметрыСобытия = ОписаниеОбработчика.Параметры;
	КонецЕсли;
	
	Если ПараметрыСобытия.Количество() > 0  Тогда
		Делегат.Исполнить(ПараметрыСобытия);
	Иначе
		Делегат.Исполнить();
	КонецЕсли;
	
КонецПроцедуры

ИндексОбработчиков = Новый Соответствие();